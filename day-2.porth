include "helpers.porth"

const BUFFER_CAP 1024 end
memory buffer BUFFER_CAP end
memory first_char sizeof(int) end
memory x sizeof(int) end
memory part1_y sizeof(int) end
memory part2_y sizeof(int) end
memory aim sizeof(int) end

while BUFFER_CAP buffer stdin read dup 0 > do
  // len
  0 while 2dup > do
    // len idx
    over swap
    // len len idx
    dup buffer +ptr @8 first_char !8
    // len len idx
    buffer swap
    // len len buf idx
    while 2dup swap +ptr @8 isdigit lnot do 1 + end
    // len len buf idx_of_digit
    parse_int
    // len idx_of_next num
    first_char @8 'f' = if // forward
      dup
      x @64 + x !64
      aim @64 *
      part2_y @64 + part2_y !64
    else first_char @8 'd' = if* // down
      dup
      part1_y @64 + part1_y !64
      aim @64 + aim !64
    else first_char @8 'u' = if* // up
      dup
      part1_y @64 swap - part1_y !64
      aim @64 swap - aim !64
    else
      drop // UNKNOWN CODE
    end
    // len idx_of_next
  end drop drop
end drop
x @64 part1_y @64 * print
x @64 part2_y @64 * print
