include "helpers.porth"

const BOARD_SIZE 5 5 * end
const BOARD_CELL_SIZE sizeof(int) sizeof(bool) + end
memory numbers sizeof(List) end
memory boards BOARD_SIZE BOARD_CELL_SIZE * 100 * end // input has 100 boards in it
memory len(boards) sizeof(int) end
memory p sizeof(ptr) end
memory bingos sizeof(bool) 100 * end // input has 100 boards in it
memory result sizeof(int) end // Save scores to this, the last one will be the one there at the end
boards p !ptr
sizeof(int) numbers list_init


memory input sizeof(Str) end
proc len(input) -- int in input @Str.count end
memory line sizeof(Str) end
proc @line -- int ptr in line @Str end
proc len(line) -- int in line @Str.count end
memory word sizeof(Str) end
proc @word -- int ptr in word @Str end
proc len(word) -- int in word @Str.count end
read_stdin_to_str @Str input !Str

proc read_numbers in
  line input str-chop-line
  while ',' word line str-chop-by-delim len(word) 0 > do
    word @Str try-parse-int drop numbers list_add
  end
end

proc dump_board int in // board_id
  "dumping board id " puts dup print
  BOARD_SIZE BOARD_CELL_SIZE * * boards +ptr
  // base_p
  0 while dup BOARD_SIZE < do
    // base_p idx
    over over BOARD_CELL_SIZE * swap +ptr
    // base_p idx cell_p
    dup @64
    swap sizeof(int) swap +ptr @bool if
      "(" puts putu ")" puts
    else
      " " puts putu " " puts
    end
    1 +
    dup 5 % 0 = if
      "\n" puts
    else
      " " puts
    end
  end drop drop
end

proc read_board -- bool in
  len(input) 0 > if
    line input str-chop-line // Skip first line
    0 while dup BOARD_SIZE < do
      dup 5 % 0 = if
        line input str-chop-line
      end
      while word line str-chop-word len(word) 0 = do end // Some vals are padded with whitespace
      word @Str try-parse-int drop
      p @ptr !64
      BOARD_CELL_SIZE p @ptr +ptr p !ptr
      1 +
    end drop
    len(boards) inc64
    true
  else
    false
  end
end

proc has_row ptr -- bool in
  // cell_p
  dup sizeof(int) swap +ptr @bool
  over BOARD_CELL_SIZE 1 * sizeof(int) + swap +ptr @bool land
  over BOARD_CELL_SIZE 2 * sizeof(int) + swap +ptr @bool land
  over BOARD_CELL_SIZE 3 * sizeof(int) + swap +ptr @bool land
  over BOARD_CELL_SIZE 4 * sizeof(int) + swap +ptr @bool land
  swap drop
end

proc has_col ptr -- bool in
  // cell_p
  dup sizeof(int) swap +ptr @bool
  over BOARD_CELL_SIZE 5 * 1 * sizeof(int) + swap +ptr @bool land
  over BOARD_CELL_SIZE 5 * 2 * sizeof(int) + swap +ptr @bool land
  over BOARD_CELL_SIZE 5 * 3 * sizeof(int) + swap +ptr @bool land
  over BOARD_CELL_SIZE 5 * 4 * sizeof(int) + swap +ptr @bool land
  swap drop
end

proc is_bingo int -- bool in // stk: board_idx -> bingo
  false swap // stk: ret board_idx
  BOARD_SIZE BOARD_CELL_SIZE * * boards +ptr // stk: ret cell_p
  0 while dup 5 < do // Loop over both rows and cols, stk: ret cell_p idx
    over over BOARD_CELL_SIZE 5 * * swap +ptr has_row if // This row is a bingo
      drop drop drop true 0 cast(ptr) 5 // return true (should break loop)
    else over over BOARD_CELL_SIZE * swap +ptr has_col if* // This col is a bingo
      drop drop drop true 0 cast(ptr) 5 // return true (should break loop)
    else // TODO: Once porth.porth allows end with if*, remove
    end // stk: ret cell_p idx
    1 +
  end drop drop // stk:
end

proc get_score int -- int in // stk: board_id -> score
  0 swap // stk: score board_id
  BOARD_SIZE BOARD_CELL_SIZE * * boards +ptr // stk: score board_base_p
  dup while over BOARD_SIZE BOARD_CELL_SIZE * swap +ptr over swap ptr< do // For each cell in this board, stk: score base_p cell_p
    sizeof(int) over +ptr @bool lnot if // This cell hasn't been called
      rot over @64 + rot rot // Increment score
    end
    BOARD_CELL_SIZE swap +ptr // Increment to next cell, stk: base_p cell_p
  end drop drop // stk:
end

